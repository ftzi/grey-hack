#import Fs from "lib/fs"
#import Std from "lib/std"
#import Lib from "lib/lib"

Setup = {}

/*
  Displays usage information and exits the program.

  @param {string} message - Optional error message to display before usage.
 */
Setup.exitWithUsage = function (message = "")
    if (message) then message = message.error.bold.LF
    message = "Usage:".bold.LF
    message = message + "  " + Std.programName + " home".LF
    message = message + "  " + Std.programName + " server"
    exit(message)
end function

/*
  Main entry point for the setup program. Parses arguments and runs
  the appropriate setup routine for home or server machines.
 */
Setup.main = function ()
    if not params.len or params[0] == "-h" then Setup.exitWithUsage()

    if (active_user != "root") then exit("You must be root to run this.")

    if (params[0] == "home") then
        password = user_input("Enter your root password to setup the Terminal with root on init:".LF, true)
        print("Setting up home machine...")
        Setup.setupHome(password)
    else if (params[0] == "server") then
        print("Setting up server machine...")
        Setup.setupServer()
    else
        Setup.exitWithUsage("Invalid parameter.")
    end if
end function

/*
  Sets up a home machine with init.d scripts, file permissions,
  and cleans up sensitive files.

  @param {string} password - The root password for terminal auto-login.
 */
Setup.setupHome = function (password)
    setupInitD = function (password)
        program = "print(""Welcome back! 0x31F01"")".LF // Random code that might help to know if the file was compromised or not. Can be later improved.
        program = program + "get_shell(""root"", """+password+""").start_terminal"
        Fs.createExecutable("/etc/init.d/start", program)
    end function

    Setup.setupHomeFilesAndPermissions()
    // Setup.setupAptSources()
    setupInitD(password)
end function

/*
  Sets up a server machine by configuring root permissions
  and removing unnecessary directories.
 */
Setup.setupServer = function ()
    Setup.setupRootPermissions()
    // Setup.setupAptSources()
    Fs.remove("/home")
    Fs.remove("/bin/sudo")
end function

/*
  Configures the root filesystem with restrictive permissions,
  setting ownership to root and removing all group/user/other access.
 */
Setup.setupRootPermissions = function ()
    Fs.absoluteRoot.set_owner("root", true)
    Fs.absoluteRoot.set_group("root", true)
    Fs.absoluteRoot.chmod("g-rwx", true)
    Fs.absoluteRoot.chmod("u-rwx", true)
    Fs.absoluteRoot.chmod("o-rwx", true)
end function

/*
  Setup.setupAptSources = function ()
     content = "{".LF
     content = content + """official_server"": true,".LF
     content = content + """sourceList"": {".LF
     content = content + """118.213.30.73"": 1542".LF
     content = content + "}".LF
     content = content + "}"
     error = Lib.aptClient.update()
     if (error) then throw("Error in apt-get update: " + error)
  end function
 */

/*
  Configures home machine files and permissions. Sets root permissions,
  grants execute access to essential binaries, removes sensitive files,
  and cleans up desktop symlinks.
 */
Setup.setupHomeFilesAndPermissions = function ()
    // As everything now requires root, we can't execute or open files on the Desktop with the cursor.
    // This removes them to remove the temptation.
    cleanDesktop = function ()
        userDir = Fs.home.get_folders[0]
        desktop = Fs.file(userDir.path.join("Desktop"))
        for file in desktop.get_files
            if (file.is_symlink) then file.delete()
        end for
    end function

    Setup.setupRootPermissions()
    Fs.file("/bin/sudo").chmod("g+x", true)
    Fs.file("/usr/bin/Terminal.exe").chmod("g+x", true)
    Fs.file("/usr/bin/Mail.exe").chmod("g+x", true)

    Fs.remove("/etc/passwd")
    Fs.remove("/home/guest")

    Fs.remove("/root/Desktop")
    Fs.remove("/root/Downloads")

    for dir in Fs.home.get_folders
        Fs.remove(dir.path.join("Config/Mail.txt"))
        Fs.remove(dir.path.join("Config/Bank.txt"))
    end for

    cleanDesktop()
end function

Setup.main()
