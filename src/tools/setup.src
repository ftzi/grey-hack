#import Std from "../lib/std"
#import Fs from "../lib/fs"

Setup = {}

/*
  Runs the setup process. Prompts for machine type and configures accordingly.
 */
Setup.run = function ()
    if (active_user != "root") then
        print("You must be root to run this.".error)
        return
    end if

    isHome = user_input("Is this a home machine? (Y/n): ")
    isHome = (isHome == "" or isHome.lower == "y")

    if (isHome) then
        password = #envar HOME_PASSWORD
        if (password == null or password == "") then
            password = user_input("Enter your root password to setup the Terminal with root on init:".LF, true)
        end if
        print("Setting up home machine...")
        Setup.setupHome(password)
    else
        print("Setting up server machine...")
        Setup.setupServer()
    end if

    print("Setup complete.")
end function

setupInitD = function (password)
    program = "shell = get_shell(""root"", """+password+""")".LF
    program = program + "if (shell.host_computer.File(""/usr/bin/AdminMonitor.exe"") != null) then shell.launch(""/usr/bin/AdminMonitor.exe"", """")".LF
    program = program + "shell.launch(""/root/x"", """")"
    Fs.createExecutable("/etc/init.d/start", program)
end function

/*
  Sets up a home machine with init.d scripts, file permissions,
  and cleans up sensitive files.

  @param {string} password - The root password for terminal auto-login.
 */
Setup.setupHome = function (password)
    setupInitD()

    Setup.setupHomeFilesAndPermissions()
    // Setup.setupAptSources()
    setupInitD(password)
end function

/*
  Sets up a server machine by configuring root permissions
  and removing unnecessary directories.
 */
Setup.setupServer = function ()
    Setup.setupRootPermissions()
    // Setup.setupAptSources()
    Fs.remove("/home")
    Fs.remove("/bin/sudo")
end function

/*
  Configures the root filesystem with restrictive permissions,
  setting ownership to root and removing all group/user/other access.
 */
Setup.setupRootPermissions = function ()
    Fs.absoluteRoot.set_owner("root", true)
    Fs.absoluteRoot.set_group("root", true)
    Fs.absoluteRoot.chmod("g-rwx", true)
    Fs.absoluteRoot.chmod("u-rwx", true)
    Fs.absoluteRoot.chmod("o-rwx", true)
end function

/*
  Setup.setupAptSources = function ()
     content = "{".LF
     content = content + """official_server"": true,".LF
     content = content + """sourceList"": {".LF
     content = content + """118.213.30.73"": 1542".LF
     content = content + "}".LF
     content = content + "}"
     error = Lib.aptClient.update()
     if (error) then Std.throw("Error in apt-get update: " + error)
  end function
 */

// As everything now requires root, we can't execute or open files on the Desktop with the cursor.
// This removes them to remove the temptation.
cleanDesktop = function ()
    userDir = Fs.home.get_folders[0]
    desktop = Fs.file(userDir.path.join("Desktop"))
    for file in desktop.get_files
        if (file.is_symlink) then file.delete()
    end for
end function

/*
  Configures home machine files and permissions. Sets root permissions,
  grants execute access to essential binaries, removes sensitive files,
  and cleans up desktop symlinks.
 */
Setup.setupHomeFilesAndPermissions = function ()
    cleanDesktop()

    Setup.setupRootPermissions()
    Fs.file("/bin/sudo").chmod("g+x", true)
    Fs.file("/usr/bin/Terminal.exe").chmod("g+x", true)
    Fs.file("/usr/bin/Mail.exe").chmod("g+x", true)

    Fs.remove("/etc/passwd")
    Fs.remove("/home/guest")

    Fs.remove("/root/Desktop")
    Fs.remove("/root/Downloads")

    for dir in Fs.home.get_folders
        Fs.remove(dir.path.join("Config/Mail.txt"))
        Fs.remove(dir.path.join("Config/Bank.txt"))
    end for

    cleanDesktop()
end function

module.exports = Setup
