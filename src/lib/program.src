/*
  Based on https://www.npmjs.com/package/commander
 */
#import Std from "std"

/*
  abs

  @type {Program}
 */
Program = {}

/*
  Creates a Program instance.

  @param {string} description
  @return {Program}
 */
Program.New = function (description = "")
    program = new Program
    program.description = description
    program.args = {}
    program.opts = {}
    program._params = []
    program._arguments = []
    program._options = []
    return program
end function

/*
  Generates and returns the usage/help text for the program.

  @return {string}
 */
Program.getUsage = function ()
    usage = ""
    usage = usage + "<b>Usage:</b> "+Std.programName
    if (self._options.len > 0) then usage = usage + " [options]"
    for argument in self._arguments
        if (argument.optional) then
            usage = "$ [$]".f(usage, argument.name)
        else
            usage = "$ <$>".f(usage, argument.name)
        end if
    end for
    usage = usage.LF.LF
    if (self.description) then usage = usage + self.description.bold.LF.LF

    maxLeftColumnLen = 0
    for argument in self._arguments
        if (argument.name.len > maxLeftColumnLen) then maxLeftColumnLen = argument.name.len
    end for
    for option in self._options
        if (option.fullname.len > maxLeftColumnLen) then maxLeftColumnLen = option.fullname.len
    end for

    if (self._arguments.len > 0) then
        usage = usage + "Arguments".bold.LF
        for argument in self._arguments
            usage = "$ $  $".f(usage, argument.name.padRight(maxLeftColumnLen), argument.description).LF
        end for
        usage = usage.LF
    end if

    if (self._options.len > 0) then
        usage = usage + "Options".bold.LF
        for option in self._options
            usage = "$ $  $".f(usage, option.fullname.padRight(maxLeftColumnLen), option.description).LF
        end for
    end if

    return usage
end function

/*
  Exits the program and prints the usage text.
 */
Program.exitWithUsage = function ()
    exit(self.getUsage)
end function

/*
  Exits the program with an error message followed by the usage text.

  @param {string} errorMessage
 */
Program.throwWithUsage = function (errorMessage)
    exit(LF+errorMessage.error.LF.LF+self.getUsage)
end function

/*
  Removes leading dashes from an option name.

  @param {string} option
  @return {string}
 */
Program._removeDashes = function (option)
    if (option.indexOf("--") == 0) then return option[2:]
    if (option.indexOf("-") == 0) then return option[1:]
    exit("Found no dashes for the option "+option)
end function

/*
  Parses positional arguments from the command line parameters.
 */
Program._parseArguments = function ()
    hadOptional = false
    for argument in self._arguments
        value = self._params.pull()
        if (argument.optional) then
            hadOptional = true
            if (value == null) then value = argument.default
        else
            if (value == null) then self.throwWithUsage("Argument $ is required.".f(argument.name))
            if (hadOptional) then self.throwWithUsage("Optional arguments must be at the end.")
        end if

        if (argument.type == "number" and value isa string) then
            value = value.to_int
            if (value isa string) then self.throwWithUsage("Argument $ requires a valid number.".f(argument.name))
        end if

        self.args[argument.name] = value
    end for
end function

/*
  Parses options (flags) from the command line parameters.

  @todo handle The option does not exist error message
 */
Program._parseOptions = function ()
    for option in self._options
        nameNoDash = self._removeDashes(option.name)
        self.opts[nameNoDash] = option.default

        indexOf = self._params.indexOf(option.name)
        if (indexOf == null) then continue

        self._params.remove(indexOf)

        value = option.default

        if (option.type == "boolean") then
            value = true
        else
            if (not self._params.hasIndex(indexOf)) then self.throwWithUsage("Option "+option.name+" requires a value.")
            value = self._params[indexOf] // Same index as the one before got removed
            self._params.remove(indexOf)
            if (option.type == "number") then
                value = value.to_int
                if (value isa string) then self.throwWithUsage("Option "+option.name+" requires a valid number.")
            end if
        end if

        self.opts[nameNoDash] = value
    end for
end function

/*
  Defines a positional argument for the program.

  @param {string} name
  @param {string} description
  @return {Argument}
 */
Program.argument = function (name, description = "")
    if (description) then description = description.ensureSuffix(".")

    obj = {"name": name, "description": description, "type": "string", "optional": false, "default": null}
    self._arguments.push(obj)

    argument = {}
    argument.number = function ()
        obj.type = "number"
        return argument
    end function
    // Implies it's `optional`.
    argument.default = function (defaultValue = null)
        if (defaultValue != null) then
            if (obj.type == "number" and not defaultValue isa number) then throw("defaultValue must be a number or null.")
            if (obj.type == "string" and not defaultValue isa string) then throw("defaultValue must be a string or null.")
        end if
        obj.optional = true
        obj.default = defaultValue
        return argument
    end function

    return argument
end function

/*
  Defines an option (flag) for the program.

  @param {string} name
  @param {string} description
  @return {Option}
 */
Program.option = function (name, description = "")
    if (description) then description = description.ensureSuffix(".")
    invalid = function ()
        exit("Invalid option name "+name+". Must be in the format ""-x"" or ""-x <value>"".")
    end function

    if (name.len < 2 or name[0] != "-") then invalid()

    splittedName = name.split(" ")
    if (splittedName.len > 2) then invalid()

    fullname = name
    name = splittedName[0]
    valueName = null
    type = "boolean"

    if (splittedName.len == 2) then
        type = "string"
        valueName = splittedName[1]
        if (valueName.len < 3 or valueName[0] != "<" or valueName[-1] != ">") then invalid()
    end if

    obj = {
        "name": name,
        "valueName": valueName,
        "fullname": fullname,
        "description": description,
        "type": type,
        "default": null,
    }
    self._options.push(obj)

    option = {}
    option.number = function ()
        obj.type = "number"
        return option
    end function
    option.default = function (value)
        if (obj.type == "boolean") then throw("Can't set a default value for a boolean option.")
        if (obj.type == "number" and not value isa number) then throw(".default must be a number.")
        if (obj.type == "string" and not value isa string) then throw(".default must be a string.")
        obj.default = value
        return option
    end function
    return option
end function

/*
  Parses command line parameters and populates args and opts.

  You can pass the params to parse, or it will use the global `params`. Useful for testing.

  @param {list} _params
 */
Program.parse = function (_params = null)
    if (not _params) then _params = params[:] // Can't _params = params in this function args
    self._params = _params
    self.option("-h", "Prints the usage.")

    self._parseOptions()
    self._parseArguments()

    if (self.opts.h) then self.exitWithUsage
end function

module.exports = Program
