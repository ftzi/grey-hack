#import Std from "std"

/*
  Extended file operations.

  @type XFile
*/
XFile = {}
XFile._file = null

/*
  Creates an XFile instance.

  @param {file} file
  @return {XFile}
 */
XFile.New = function (file)
    xfile = new XFile
    xfile._file = file
    return xfile
end function

/*
  Returns the path of the file.

  @return {string}
 */
XFile.path = function()
    return self._file.path
end function

/*
  Returns the content of the file.

  @return {string}
 */
XFile.get_content = function()
    return self._file.get_content
end function

/*
  Checks if the file has the specified permission.

  @param {string} permission
  @return {boolean}
 */
XFile.has_permission = function(permission)
    return self._file.has_permission(permission)
end function

/*
  Returns the name of the file.

  @return {string}
 */
XFile.name = function()
    return self._file.name
end function

/*
  Returns all subdirectories as XFile instances.

  @return {list<XFile>}
 */
XFile.get_folders = function()
    return self._file.get_folders.map(@XFile.New)
end function

/*
  Navigates to the given path by using the file as starting point.
  The path can start with `/` to be absolute or without to be relative.

  @param {string} path
  @return {XFile|null}
 */
XFile.getAt = function (path)
    file = self._file
    isPathAbsolute = path[0] == "/"
    absolutePath = path
    if (not isPathAbsolute) then absolutePath = file.path.join(path)
    while (file.path != "/")
        file = file.parent
    end while

	segmentsToNavigate = path.replace("/", " ").trim().split(" ")
	for segment in segmentsToNavigate
		files = file.get_folders + file.get_files
		for f in files
			if (f.name == segment) then
                file = f
                break
            end if
		end for
	end for
	if (file.path == absolutePath) then return XFile.New(file) else return null
end function

/*
  Returns the content of the file at the given path.
  Returns null if file not found or read permission denied.

  @param {string} path
  @return {string|null}
 */
XFile.contentAt = function (path)
    xfile = self.getAt(path)
    if (xfile and xfile.has_permission("r")) then return xfile.get_content()
    return null
end function

/*
  @return {string} The current user for the given file's context.
 */
XFile.user = function()
    root = self.getAt("/")
    if root.has_permission("w") then return "root"

	home = self.getAt("/home")
    for dir in home.get_folders
        if dir.name == "guest" then continue
        if dir.has_permission("w") then return dir.name
    end for

    return "guest"
end function

module.exports = XFile
