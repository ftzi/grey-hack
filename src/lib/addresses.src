#import Kv from "kv"
#import Lib from "lib"

/*
  Manages Metaxploit scan addresses for libraries.

  @type Addresses
*/
Addresses = {}
Addresses._addresses = null
Addresses.kv = Kv.New("/root/addresses.kv", true)

/*
  Loads saved addresses from the CSV file.

  @return {map}
 */
Addresses._loadSaved = function ()
    addresses = Addresses.kv.read()
    if (not addresses) then return {}

    obj = {}
    for address in addresses
        libName_version = address[0].split("@")
        addressStrings = address[1].split(",")
        addressList = []
        for addrStr in addressStrings
            addressList.push(addrStr.to_int)
        end for
        obj.set([libName_version[0], libName_version[1]], addressList)
    end for
    return obj
end function

/*
  Returns the cached addresses, loading from file if not yet loaded.

  @return {map}
 */
Addresses.addresses = function ()
    if (Addresses._addresses == null) then Addresses._addresses = Addresses._loadSaved()
    return Addresses._addresses
end function

/*
  Checks if local addresses exist for a given library.

  @param {map} lib - A library object
  @return {boolean}
 */
Addresses.hasLocalAddressesFor = function (lib)
    return Addresses.addresses.get([lib.lib_name, lib.version]) != null
end function

/*
  Scans a library for addresses using Metaxploit and saves them to the CSV file.

  @param {map} lib - The library object to scan
  @return {list}
 */
Addresses.scan = function (lib)
    print("Scanning Addresses for <b>$ $</b> using Metaxploit $.".f(lib.lib_name, lib.version, Lib.versionOf("metaxploit")))
    addresses = Lib.metaxploit.scan(lib)
    print("Adding new addresses for $ $ to $".f(lib.lib_name, lib.version, "/root/addresses.kv"))
    addressesStr = addresses.join(",")
    Addresses.kv.setKv([lib.lib_name + "@" + lib.version, addressesStr])
    Addresses.addresses.set([lib.lib_name, lib.version], addresses)
    return addresses
end function

/*
  Gets addresses for a library, scanning if not already cached.

  @param {map} lib - A library object
  @return {list}
 */
Addresses.addressesFor = function (lib)
    addresses = Addresses.addresses.get([lib.lib_name, lib.version])
    if (not addresses) then addresses = Addresses.scan(lib)

    return addresses
end function

module.exports = Addresses
