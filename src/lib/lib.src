/*
  Map containing various libs that are lazy loaded and utility functions.

  @type {Lib}
 */
Lib = {}

Lib._libs = {}

Lib._getLib = function (name)
    if (not Lib._libs.hasIndex(name)) then Lib._libs[name] = Lib.load(name)
    return Lib._libs[name]
end function

/*
  @return {metaxploit}
 */
Lib.metaxploit = function()
    return Lib._getLib("metaxploit")
end function

/*
  @return {crypto}
 */
Lib.crypto = function()
    return Lib._getLib("crypto")
end function

/*
  @return {aptClient}
 */
Lib.aptClient = function()
    return Lib._getLib("aptclient")
end function

/*
  @param {string} name
  @param {boolean} installIfMissing
  @return {string}
 */
Lib.libPath = function (name, installIfMissing)
    libPath = "/lib/" + name
    if (get_shell.host_computer.File(libPath)) then return libPath
    localLibPath = current_path + "/" + name
    if (get_shell.host_computer.File(localLibPath)) then return localLibPath
    if (not installIfMissing) then
        throw("Can't find the <b>" + name + "</b> lib in the /lib path or the current folder.")
    end if

    error = Lib.aptClient.install(name)
    if (error) then throw ("Error installing the missing library "+name+": "+error)
    return libPath
end function

/*
  Returns the version of a local library

  @param {string} name
  @return {string}
 */
Lib.versionOf = function (name)
    if (name.indexOf(".so") == null) then name = name + ".so"
    return Lib.metaxploit.load(Lib.libPath(name, false)).version
end function

/*
  Loads a lib either by reading it from the "/lib" or from the current path.
  No need to provide the lib extension (".so").

  @param {string} name
  @return {object}
 */
Lib.load = function (name)
    if (name.indexOf(".so") == null) then name = name + ".so"
    lib = include_lib(Lib.libPath(name, true))
    if not lib then exit("Error loading the lib "+name)
    return lib
end function

/*
  @param {string} ip
  @param {number} port
  @return {metaLib}
 */
Lib.loadRemote = function (ip, port)
    net = Lib.metaxploit.net_use(ip, port)
    if not net then exit("Error: can't connect to net session")
    if (net.is_root_active_user) then exit("Root user is active!")
    return net.dump_lib
end function
