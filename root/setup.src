import_code("/root/c.src")
Compile.selfCompile()

import_code("/root/utils/fs")
import_code("/root/utils/std")

Setup = {}

// TODO program lib
Setup.exitWithUsage = function (message = "")
    if (message) then message = message.error.bold.LF
    message = "Usage:".bold.LF
    message = message + "  " + Consts.programName + " home".LF
    message = message + "  " + Consts.programName + " server"
    exit(message)
end function

Setup.main = function ()
    if not params.len or params[0] == "-h" then Setup.exitWithUsage()

    if (active_user != "root") then exit("You must be root to run this.")

    if (params[0] == "home") then
        password = user_input("Enter your root password to setup the Terminal with root on init:".LF, true)
        print("Setting up home machine...")
        Setup.setupHome(password)
    else if (params[0] == "server") then
        print("Setting up server machine...")
        Setup.setupServer()
    else
        Setup.exitWithUsage("Invalid parameter.")
    end if
end function

Setup.setupCommon = function ()
    Fs.absoluteRoot.set_owner("root", true)
    Fs.absoluteRoot.set_group("root", true)
    Fs.absoluteRoot.chmod("g-rwx", true)
    Fs.absoluteRoot.chmod("u-rwx", true)
    Fs.absoluteRoot.chmod("o-rwx", true)
end function

Setup.setupHome = function (password)
    Setup.setupCommon()
    Fs.getFile("/bin/sudo").chmod("g+x", true)
    Fs.getFile("/usr/bin/Terminal.exe").chmod("g+x", true)
    Fs.remove("/etc/passwd")
    Fs.remove("/home/guest")

    Fs.remove("/root/Desktop")
    Fs.remove("/root/Downloads")

    for dir in Fs.home.get_folders
        Fs.remove(dir.path.join("Config/Mail.txt"))
        Fs.remove(dir.path.join("Config/Bank.txt"))
    end for

    Setup.setupInitD(password)
    Setup.cleanDesktop()
end function

// As everything now requires root, we can't execute or open files on the Desktop with the cursor.
// This removes them to remove the temptation.
Setup.cleanDesktop = function ()
    userDir = Fs.home.get_folders[0]
    desktop = Fs.getFile(userDir.path.join("Desktop"))
    for file in desktop.get_files
        if (file.is_symlink) then file.delete()
    end for
end function

Setup.setupServer = function ()
    Setup.setupCommon()
    Fs.remove("/home")
end function

Setup.setupInitD = function (password)
    Fs.createExecutable("/etc/init.d/start", "get_shell(""root"", """+password+""").start_terminal")
end function

Setup.setupAptSources = function ()
    content = "{\n"
    content = content + """official_server"": true,\n"
    content = content + """sourceList"": {\n"
    content = content + """118.213.30.73"": 1542\n"
    content = content + "}\n"
    content = content + "}"
end function

Setup.main()
