import_code("/root/c.src")
Compile.selfCompile()

import_code("/root/utils/program")
import_code("/root/utils/fs")

Debug = {}
Debug.Result = {
    "file": null,
    "line": null,
    "lineContent": null,
    "error": null,
}
// @param {string} entryFilePath
// @param {number} targetLineNumber
// @return {FindLine|null}
Debug.findLine = function (entryFilePath, targetLineNumber)
    entryFilePath = entryFilePath.ensureSuffix(".src").absolutePath()
    if (targetLineNumber < 1) then throw("The target line number must be greater than 0.")

    state = {}
    state.totalLineCount = 0

    recursive = function(filePath)
        fileContent = Fs.read(filePath)
        currentFileLineCount = 0
        for line in fileContent.split(LF)
            state.totalLineCount = state.totalLineCount + 1
            currentFileLineCount = currentFileLineCount + 1

            if (state.totalLineCount == targetLineNumber) then
                result = new Debug.Result
                result.filePath = filePath
                result.line = currentFileLineCount
                result.lineContent = line
                return result
            end if

            importPath = line.valueBetween("import_cod"+"e(""", """")
            if not importPath then continue
            result = recursive(importPath.ensureSuffix(".src"))
            if (result) then
                return result
            end if
        end for
    end function

    result = recursive(entryFilePath)

    if (result) then return result
    result = new Debug.Result
    result.error = "Target line $ is greater than the total line count ($).".f(targetLineNumber, state.totalLineCount)
    return result
end function

Debug.main = function ()
    program = Program.New
    program.argument("srcFile", "The main program")
    program.argument("targetLine", "The line number to find the corresponding source file which raised the runtime error.").number
    program.parse()

    result = Debug.findLine(program.args.srcFile, program.args.targetLine)
    if (result.error) then
        throw(result.error)
    end if
    print("Found the line $ at $@$ with the content:".f(program.args.targetLine.bold, result.filePath.bold, result.line.bold).color("green"))
    print(""""+result.lineContent.trim.italic.bold+"""")
end function

if (Consts.programName == "debug") then Debug.main()
