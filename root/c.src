// Compiler program. Compile not only the entry file but also the sources of the executables imported as well.
//
// This intentionally has no imports, so no file needs to be built before building this.
// Simply run `build c.src /root` to build this and then `c` to build everything in `/root`.

Compile = {}
// Files which were accessed.
Compile.accessed = []
Compile.srcToTryToTest = []
Compile.optionToSkip = "--skipSelfCompile"

// @param {string}
// @return {string} path of the executable entry file
Compile.compile = function (entryFilePath)
    if (entryFilePath[0] != "/") then entryFilePath = current_path + "/" + entryFilePath

    valuesBetween = function (string, open, close)
        array = []
        head = null
        tail = null
        while true
            head = string.indexOf(open, tail)
            if (head == null) then break
            tail = string.indexOf(close, head + open.len)
            if (tail == null) then break
            array.push(string[head + open.len:tail])
        end while

        return array
    end function

    // Only adds if the list doesn't have this item yet.
    addUnique = function (list, item)
        if (list.indexOf(item) == null) then list.push(item)
    end function

    recursive = function(filePath)
        if (filePath.indexOf(".src") == null) then filePath = filePath + ".src"

        // Skip this file if already accessed
        if (Compile.accessed.indexOf(filePath) != null) then return

        Compile.accessed.push(filePath)
        addUnique(Compile.srcToTryToTest, filePath)

        file = get_shell.host_computer.File(filePath)
        if (not file) then exit("<color=red>File not found: " + filePath + "</color>")

        fileContent = file.get_content()
        if (fileContent == null) then exit("<color=red>Coulnd't read the file " + file.path + "</color>")

        imports = valuesBetween(fileContent, "import_" + "code(""", """")

        for import in imports
            // We don't need to compile ".src" imports. Skip them!
            if (import.indexOf(".src") != null) then
                addUnique(Compile.srcToTryToTest, filePath)
                continue
            end if
            recursive(import)
        end for

        allowImport = filePath != entryFilePath
        buildResult = get_shell.build(file.path, file.parent.path, allowImport)
        if (buildResult) then
            exit("<color=red>Error building the file "+file.path+": " + buildResult+"</color>")
        end if
        return file.path.remove(".src")
    end function

    executablePath = recursive(entryFilePath)
    return executablePath
end function

// Makes the program build itself with the latest .src, if available, to improve DX.
// Passing --skipSelfCompile while calling the program skips this.
// This file should be imported first and this function should be called right after it.
Compile.selfCompile = function ()
    srcPath = program_path+".src"

    // Skip self compile if there is no source file for the entry file.
    if (get_shell.host_computer.File(srcPath) == null) then return

    indexOfSkip = params.indexOf(Compile.optionToSkip)
    // Skip if the optionToSkip is passed.
	if (indexOfSkip != null) then
        params.remove(indexOfSkip) // So it's not present in the final program if -x.
        return
    end if

	Compile.compile(srcPath)

    // Relaunch
	get_shell.launch(program_path, (params + [Compile.optionToSkip]).join(" "))
	exit() // So the rest of the original program isn't run.
end function

Compile._main = function ()
    if (params.len > 0 and params[0] == "-h") then exit("<b>Usage:c [-x] [entryFile] [programArgs]</b>\n-x executes the file after compiling. If entry file is omitted, compiles the whole cwd.")

    executeAfter = false

    compileAllFilesInDir = params.len == 0
    if (compileAllFilesInDir) then
        print("Compiling all the source files in the current directory "+current_path+"...")
        count = 0
        for file in get_shell.host_computer.File(current_path).get_files
            if (file.name.indexOf(".src") == null) then continue
            Compile.compile(file.path)
            count = count + 1
        end for
        print("Successfully compiled "+count+" files.")
    else
        executeAfter = params[0] == "-x"
        if (executeAfter) then params.remove(0)

        entryFile = params[0]
        params.remove(0)

        executablePath = Compile.compile(entryFile)
    end if

    Compile.runTests()
    if (executeAfter) then get_shell.launch(executablePath, params.join(" "))
end function

// Run the tests, if any. NOTE:
Compile.runTests = function ()
    print("Now running tests.")
    for src in Compile.srcToTryToTest
        src = src.remove(".src") + ".test.src"

        file = get_shell.host_computer.File(src)
        if (not file) then continue
        print("Running test " + file.path)
        executableTest = Compile.compile(file.path)
    end for
end function

if (program_path.split("/")[-1] == "c") then
    Compile.selfCompile()
    Compile._main()
end if
